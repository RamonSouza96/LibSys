unit User.Controller;

interface

uses
  FireDAC.Comp.Client, System.SysUtils, System.Classes, Data.DB, FMX.Types, FireDAC.stan.Param,
  FireDAC.Comp.DataSet, FireDAC.Stan.Def, FireDAC.Phys.SQLite, FireDAC.Phys.SQLiteDef, System.Types,
  System.Generics.Collections, FMX.Dialogs, System.TypInfo;

type
  TUserData = record
    Id: Integer;
    Nome: string;
    Email: String;
    Tipo: Integer;
    Result: string;
    Usuario: string;
    Senha: string;
    Salt: string;
    Status: Boolean;
    UserList: TArray<TUserData>;
  end;

  TUserController = class
  public
    class function GetUserById(AId: Integer): TUserData;
    class procedure SaveUser(var UserData: TUserData);
    class procedure UpdateUser(var UserData: TUserData);
    class procedure DeleteUser(AId: Integer);
    class function IsLoginExists(ALogin: string): Boolean;
  end;

implementation

uses
  Dm.Utils;

{ TUserController }

class function TUserController.GetUserById(AId: Integer): TUserData;
const
  LSql =
    'SELECT ' +
    '  USU_ID, ' +
    '  USU_NOME, ' +
    '  USU_LOGIN, ' +
    '  USU_SENHA, ' +
    '  USU_EMAIL, ' +
    '  USU_ATIVO, ' +
    '  USU_SALT, ' +
    '  USU_TIPO_USUARIO, ' +
    '  USU_DATA_INATIVO ' +
    'FROM ' +
    '  Usuarios';
var
  LResultData: TUserData;
  LQry: TFDQuery;
  LCon: TFDConnection;
  LUserList: TList<TUserData>;
begin
  LCon := TDataUtils.CreateConnection;
  LCon.Connected := True;
  LQry := TFDQuery.Create(nil);
  LQry.Connection := LCon;
  LUserList := TList<TUserData>.Create;
  try
    with LQry do
    begin
      Close;
      SQL.Clear;
      SQL.Add(LSql);
      if AId <> 0 then
      begin
        SQL.Add('WHERE USU_ID = :pId');
        ParamByName('pId').AsInteger := AId;
      end;
      Prepared := True;
      Open;

      if RecordCount > 0 then
      begin
        if AId = 0 then
        begin
          while not Eof do
          begin
            LResultData.Id := FieldByName('USU_ID').AsInteger;
            LResultData.Nome := FieldByName('USU_NOME').AsString;
            LResultData.Email := FieldByName('USU_EMAIL').AsString;
            LResultData.Tipo := FieldByName('USU_TIPO_USUARIO').AsInteger;
            LResultData.Usuario := FieldByName('USU_NOME').AsString;
            LResultData.Senha := '';
            LResultData.Status := FieldByName('USU_ATIVO').AsBoolean;
            LUserList.Add(LResultData);

            Next;
          end;

          LResultData.UserList := LUserList.ToArray;
        end
        else
        begin
          LResultData.Id := FieldByName('USU_ID').AsInteger;
          LResultData.Nome := FieldByName('USU_NOME').AsString;
          LResultData.Email := FieldByName('USU_EMAIL').AsString;
          LResultData.Tipo := FieldByName('USU_TIPO_USUARIO').AsInteger;
          LResultData.Usuario := FieldByName('USU_NOME').AsString;
          LResultData.Senha := '';
          LResultData.Status := FieldByName('USU_ATIVO').AsBoolean;
        end;
      end;
    end;
  finally
    LQry.Close;
    LQry.DisposeOf;
    LCon.Connected := False;
    LCon.DisposeOf;
    LUserList.Free;
  end;

  Result := LResultData;
end;

class procedure TUserController.SaveUser(var UserData: TUserData);
var
  LCon: TFDConnection;
  LQry: TFDQuery;
  LSql: string;
begin
  LCon := TDataUtils.CreateConnection;
  LCon.Connected := True;
  LQry := TFDQuery.Create(nil);
  LQry.Connection := LCon;
  try
    if UserData.Id = 0 then
    begin
      LSql :=
        'INSERT INTO Usuarios (USU_NOME, USU_LOGIN, USU_SENHA, USU_EMAIL, USU_ATIVO, USU_SALT, USU_TIPO_USUARIO, USU_DATA_CADASTRO, USU_DATA_INATIVO) ' +
        'VALUES (:NOME, :LOGIN, :SENHA, :EMAIL, :ATIVO, :SALT, :TIPO_USUARIO, :USU_DATA_CADASTRO, :USU_DATA_INATIVO)';

      with LQry do
      begin
        Close;
        SQL.Clear;
        SQL.Add(LSql);
        ParamByName('NOME').AsString := UserData.Nome;
        ParamByName('LOGIN').AsString := UserData.Usuario;
        ParamByName('SENHA').AsString := UserData.Senha;
        ParamByName('EMAIL').AsString := UserData.Email;
        ParamByName('ATIVO').AsBoolean := UserData.Status;
        ParamByName('SALT').AsString := UserData.Salt;
        ParamByName('TIPO_USUARIO').AsInteger := UserData.Tipo;
        ParamByName('USU_DATA_CADASTRO').AsDate := Date;
        if not UserData.Status then
          ParamByName('USU_DATA_INATIVO').AsString := DateToStr(Date)
        else
          ParamByName('USU_DATA_INATIVO').AsString := ''; // Limpa o parâmetro se o usuário estiver ativo

        ExecSQL;
      end;
    end;
  finally
    LQry.Close;
    LQry.DisposeOf;
    LCon.Connected := False;
    LCon.DisposeOf;
  end;
end;

class procedure TUserController.UpdateUser(var UserData: TUserData);
var
  LSql: string;
  LCon: TFDConnection;
  LQry: TFDQuery;
  FieldName: string;
begin
  LSql := 'UPDATE Usuarios SET';

  if UserData.Nome <> '' then
  begin
    LSql := LSql + ' USU_NOME = :Nome,';
  end;
  if UserData.Usuario <> '' then
  begin
    LSql := LSql + ' USU_LOGIN = :Usuario,';
  end;
  if UserData.Senha <> '' then
  begin
    LSql := LSql + ' USU_SENHA = :Senha,';
  end;
  if UserData.Email <> '' then
  begin
    LSql := LSql + ' USU_EMAIL = :Email,';
  end;
  if UserData.Status <> False then
  begin
    LSql := LSql + ' USU_ATIVO = :Status,';
  end;
  if UserData.Salt <> '' then
  begin
    LSql := LSql + ' USU_SALT = :Salt,';
  end;
  if UserData.Tipo <> 0 then
  begin
    LSql := LSql + ' USU_TIPO_USUARIO = :Tipo,';
  end;
  if not UserData.Status then
  begin
    LSql := LSql + ' USU_DATA_INATIVO = :DataInativo,';
  end;

  // Remove a última vírgula
  if LSql.EndsWith(',') then
    LSql := LSql.Remove(LSql.Length - 1);

  LSql := LSql + ' WHERE USU_ID = :Id';

  LCon := TDataUtils.CreateConnection;
  LCon.Connected := True;
  LQry := TFDQuery.Create(nil);
  LQry.Connection := LCon;
  try
    with LQry do
    begin
      Close;
      SQL.Clear;
      SQL.Add(LSql);

      if UserData.Nome <> '' then
        ParamByName('Nome').AsString := UserData.Nome;
      if UserData.Usuario <> '' then
        ParamByName('Usuario').AsString := UserData.Usuario;
      if UserData.Senha <> '' then
        ParamByName('Senha').AsString := UserData.Senha;
      if UserData.Email <> '' then
        ParamByName('Email').AsString := UserData.Email;
      if UserData.Status <> False then
        ParamByName('Status').AsBoolean := UserData.Status;
      if UserData.Salt <> '' then
        ParamByName('Salt').AsString := UserData.Salt;
      if UserData.Tipo <> 0 then
        ParamByName('Tipo').AsInteger := UserData.Tipo;
      if not UserData.Status then
        ParamByName('DataInativo').AsString := DateToStr(Date);

      ParamByName('Id').AsInteger := UserData.Id;

      ExecSQL;
    end;
  finally
    LQry.DisposeOf;
    LCon.Connected := False;
    LCon.DisposeOf;
  end;
end;

class procedure TUserController.DeleteUser(AId: Integer);
var
  LCon: TFDConnection;
  LQry: TFDQuery;
  LSql: string;
begin
  LCon := TDataUtils.CreateConnection;
  LCon.Connected := True;
  LQry := TFDQuery.Create(nil);
  LQry.Connection := LCon;
  try
    LSql :=
      'DELETE FROM Usuarios WHERE USU_ID = :ID';

    with LQry do
    begin
      Close;
      SQL.Clear;
      SQL.Add(LSql);
      ParamByName('ID').AsInteger := AId;
      ExecSQL;
    end;
  finally
    LQry.Close;
    LQry.DisposeOf;
    LCon.Connected := False;
    LCon.DisposeOf;
  end;
end;

class function TUserController.IsLoginExists(ALogin: string): Boolean;
var
  LCon: TFDConnection;
  LQry: TFDQuery;
  LSql: string;
begin
  LCon := TDataUtils.CreateConnection;
  LCon.Connected := True;
  LQry := TFDQuery.Create(nil);
  LQry.Connection := LCon;
  try
    LSql :=
      'SELECT COUNT(*) FROM Usuarios WHERE USU_LOGIN = :LOGIN';

    with LQry do
    begin
      Close;
      SQL.Clear;
      SQL.Add(LSql);
      ParamByName('LOGIN').AsString := ALogin;
      Open;
      Result := Fields[0].AsInteger > 0;
    end;
  finally
    LQry.Close;
    LQry.DisposeOf;
    LCon.Connected := False;
    LCon.DisposeOf;
  end;
end;

end.

